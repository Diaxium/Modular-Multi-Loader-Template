// #:Root Build.gradle

plugins {
    // see https://fabricmc.net/develop/ for new versions
    id 'fabric-loom' version '1.10-SNAPSHOT' apply false
    // see https://projects.neoforged.net/neoforged/moddevgradle for new versions
    id 'net.neoforged.moddev' version '1.0.11' apply false
}

subprojects {
    // Centralized Build Artifacts
    layout.buildDirectory = layout.projectDirectory.dir(
            name == 'api' || name == 'utils'
                    ? "${rootProject.layout.buildDirectory.get()}/core/${name}"
                    : (name in ['fabric', 'forge', 'neoforge'])
                    ? "${rootProject.layout.buildDirectory.get()}/core/app/platform/${name}"
                    : "${rootProject.layout.buildDirectory.get()}/core/app/${name}"
    )

    // Centralized Documentation
    tasks.withType(Javadoc).configureEach {
        setDestinationDir(rootProject.layout.projectDirectory.dir("docs/${project.name}").asFile)
    }
}

tasks.register("aggregateJavadoc", Javadoc) {
    group = "documentation"
    description = "Generates Javadoc for all subprojects in a centralized directory."

    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    setDestinationDir(rootProject.layout.projectDirectory.dir("docs/aggregate").asFile)
}